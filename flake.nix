{
  description = "zmx - session persistence for terminal processes";

  inputs = {
    zig2nix.url = "github:Cloudef/zig2nix";
  };

  outputs =
    { zig2nix, ... }:
    let
      flake-utils = zig2nix.inputs.flake-utils;
    in
    (flake-utils.lib.eachSystem [ "x86_64-linux" "aarch64-linux" "aarch64-darwin" "x86_64-darwin" ] (
      system:
      let
        env = zig2nix.outputs.zig-env.${system} {
          zig = zig2nix.outputs.packages.${system}.zig-0_16_0;
        };
      in
      with builtins;
      with env.pkgs.lib;
      let
        isDarwin = env.pkgs.stdenv.hostPlatform.isDarwin;
        sdkRoot = env.pkgs.apple-sdk.sdkroot;
        xcrunWrapper = env.pkgs.writeShellScriptBin "xcrun" ''
          echo "${sdkRoot}"
        '';
        xcodeselectWrapper = env.pkgs.writeShellScriptBin "xcode-select" ''
          echo "${sdkRoot}"
        '';

        zmx-package = env.package (
          {
            src = cleanSource ./.;
            zigBuildFlags = [ "-Doptimize=ReleaseSafe" ];
            zigPreferMusl = !isDarwin;
          }
          // optionalAttrs isDarwin {
            glibc = null;
            musl = null;
            nativeBuildInputs = [
              xcrunWrapper
              xcodeselectWrapper
            ];
          }
        );
      in
      {
        packages = {
          zmx = zmx-package;
          default = zmx-package;
        };

        apps = {
          zmx = {
            type = "app";
            program = "${zmx-package}/bin/zmx";
          };
          default = {
            type = "app";
            program = "${zmx-package}/bin/zmx";
          };

          build = env.app [ ] "zig build \"$@\"";

          test = env.app [ ] "zig build test -- \"$@\"";
        };

        devShells.default = env.mkShell {
        };
      }
    ));
}
